plugins {
  id "com.github.ben-manes.versions" version "0.46.0"
  id 'java-library'
  id 'application'
  id 'maven-publish'
  id 'signing'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

ext.junitJupiterVersion = '5.9.2'

group = 'io.calimero'
version = '3.0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

java {
    withSourcesJar()
    withJavadocJar()

    registerFeature('serial') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('usb') {
        usingSourceSet(sourceSets.main)
    }
}

tasks.withType(Jar).configureEach {
    from("${projectDir}") {
        include 'LICENSE.txt'
        into "META-INF"
    }
    if (name.equals('sourcesJar')) {
        from("${projectDir}") {
            include 'README.md'
        }
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

application {
    mainModule = "io.calimero.server"
    mainClass = 'io.calimero.server.Launcher'
}

run {
    standardInput = System.in
}

compileJava.options*.compilerArgs = [
	'-Xlint:all,-serial'
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]

tasks.named('compileJava') {
    options.javaModuleVersion = provider { project.version }
}

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDirs = [ 'test' ]
        file('build/classes/test').mkdirs()
        runtimeClasspath = runtimeClasspath + files('build/classes/test')
    }
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption("Xdoclint:-missing", '-quiet')
}

jar {
	manifest {
		attributes 'Main-Class': application.mainClass
	}
}

test {
	useJUnitPlatform()
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += [
            "--add-reads", "io.calimero.core=io.calimero.server", // @LinkEvent
            "--add-reads", "io.calimero.serial.provider.rxtx=ALL-UNNAMED",
            "--add-reads", "io.calimero.usb.provider.javax=ALL-UNNAMED"]
}

dependencies {
    api "io.calimero:calimero-core:${version}"
    api "io.calimero:calimero-device:${version}"

    implementation 'org.slf4j:slf4j-api:1.7.36'
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.36'

	serialRuntimeOnly "io.calimero:calimero-rxtx:${version}"
	serialRuntimeOnly "io.calimero:serial-native:${version}"
	usbRuntimeOnly "io.calimero:calimero-usb:${version}"

	testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'Calimero KNXnet/IP Server'
                description = 'Calimero KNXnet/IP server is a free KNX network server'
                url = 'https://github.com/calimero-project/calimero-server'
                inceptionYear = '2006'
                licenses {
                    license {
                        name = "GNU General Public License, version 2, with the Classpath Exception"
                        url = 'LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        name = "Boris Malinowsky"
                        email = "b.malinowsky@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/calimero-project/calimero-server.git'
                    url = 'https://github.com/calimero-project/calimero-server.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'maven'
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}

signing {
    if (project.hasProperty("signing.keyId")) {
        sign publishing.publications.mavenJava
    }
}
