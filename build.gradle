plugins {
  id "com.github.ben-manes.versions" version "0.49.0"
  id 'java-library'
  id 'application'
  id 'maven-publish'
  id 'signing'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

ext.junitJupiterVersion = '5.10.1'

group = 'io.calimero'
version = '3.0-SNAPSHOT'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
    withSourcesJar()
    withJavadocJar()

    registerFeature('serial') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('usb') {
        usingSourceSet(sourceSets.main)
    }
}

tasks.withType(Jar).configureEach {
    from("${projectDir}") {
        include 'LICENSE.txt'
        into "META-INF"
    }
    if (name == 'sourcesJar') {
        from("${projectDir}") {
            include 'README.md'
        }
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

application {
    mainModule = "io.calimero.server"
    mainClass = 'io.calimero.server.Launcher'
}

run {
    standardInput = System.in
}

compileJava.options*.compilerArgs = [
	'-Xlint:all,-serial'
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]

tasks.named('compileJava') {
    options.javaModuleVersion = provider { project.version }
}

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDirs = [ 'test' ]
        file('build/classes/test').mkdirs()
        runtimeClasspath = runtimeClasspath + files('build/classes/test')
    }
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption("Xdoclint:-missing", '-quiet')
}

test {
	useJUnitPlatform()
}

def addReads = [ "--add-reads", "io.calimero.core=io.calimero.server", // @LinkEvent
                 "--add-reads", "io.calimero.serial.provider.rxtx=ALL-UNNAMED",
                 "--add-reads", "io.calimero.usb.provider.javax=ALL-UNNAMED" ]

tasks.withType(JavaExec).configureEach {
    jvmArgs += addReads
}

startScripts {
    defaultJvmOpts = addReads

	doLast {
		// put commons-lang jar also on the classpath, used by libusb4java (which is in UNNAMED module)
		def commonsLang3 = configurations.runtimeClasspath.filter { it.name.startsWith("commons-lang3") }
		def libName = commonsLang3.getSingleFile().name

        def unixScriptFile = file getUnixScript()
        def windowsScriptFile = file getWindowsScript()

        unixScriptFile.text = unixScriptFile.text.replace('\n\nMODULE_PATH=', ':$APP_HOME/lib/' + "${libName}\n\nMODULE_PATH=")
        windowsScriptFile.text = windowsScriptFile.text.replace('\r\nset MODULE_PATH=', ';%APP_HOME%\\lib\\' + "${libName}\r\nset MODULE_PATH=")
	}
}

dependencies {
    api "io.calimero:calimero-core:${version}"
    api "io.calimero:calimero-device:${version}"

	serialRuntimeOnly "io.calimero:calimero-rxtx:${version}"
	serialRuntimeOnly "io.calimero:serial-native:${version}"
	usbRuntimeOnly "io.calimero:calimero-usb:${version}"

	runtimeOnly 'org.slf4j:slf4j-jdk-platform-logging:2.0.9'
	runtimeOnly 'org.slf4j:slf4j-simple:2.0.9'

	testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
}

jar {
	manifest {
		attributes(
			'Main-Class': application.mainClass,
			'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' ')
		)
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'Calimero KNXnet/IP Server'
                description = 'Calimero KNXnet/IP server is a free KNX network server'
                url = 'https://github.com/calimero-project/calimero-server'
                inceptionYear = '2006'
                licenses {
                    license {
                        name = "GNU General Public License, version 2, with the Classpath Exception"
                        url = 'LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        name = "Boris Malinowsky"
                        email = "b.malinowsky@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/calimero-project/calimero-server.git'
                    url = 'https://github.com/calimero-project/calimero-server.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'maven'
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}

signing {
    if (project.hasProperty("signing.keyId")) {
        sign publishing.publications.mavenJava
    }
}
